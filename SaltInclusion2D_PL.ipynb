{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## PREPARATIONS BEFORE PARALLEL\n",
    "const USE_GPU     = false                       # Whether to use GPU\n",
    "using ParallelStencil\n",
    "using ParallelStencil.FiniteDifferences2D\n",
    "@static if USE_GPU\n",
    "    @init_parallel_stencil(CUDA,    Float64, 2)\n",
    "else\n",
    "    @init_parallel_stencil(Threads, Float64, 2)\n",
    "end\n",
    "using Images, FileIO, Interpolations, ColorTypes, TestImages, Plots, ImageTransformations\n",
    "\n",
    "\n",
    "\n",
    "## CREATE PARALLEL FUNCTIONS TO BE USED IN THE PSEUDO-TRANSIENT LOOP\n",
    "@parallel function compute_Vgrad!(dVxdx::Data.Array, dVydy::Data.Array, dVxdy::Data.Array, dVydx::Data.Array, Vx::Data.Array, Vy::Data.Array, dx::Data.Number, dy::Data.Number)\n",
    "    @all(dVxdx)   = @d_xa(Vx)/dx\n",
    "    @all(dVydy)   = @d_ya(Vy)/dy\n",
    "    @all(dVxdy)   = @d_ya(Vx)/dy\n",
    "    @all(dVydx)   = @d_xa(Vy)/dx\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function compute_P!(divV::Data.Array, P::Data.Array, dVxdx::Data.Array, dVydy::Data.Array, Gdτ::Data.Array, r::Data.Number, dx::Data.Number, dy::Data.Number)\n",
    "    @all(divV)    = @av_ya(dVxdx) + @av_xa(dVydy)\n",
    "    @all(P)       = @all(P) - r*@all(Gdτ)*@all(divV)\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function compute_E!(Exx::Data.Array, Eyy::Data.Array, Exy::Data.Array, dVxdx::Data.Array, dVydy::Data.Array, dVxdy::Data.Array, dVydx::Data.Array)\n",
    "    @all(Exx)     = @av_ya(dVxdx)\n",
    "    @all(Eyy)     = @av_xa(dVydy)\n",
    "    @all(Exy)     = 0.5 * ( @av_xa(dVxdy) + @av_ya(dVydx) )\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function compute_τ!(τxx::Data.Array, τyy::Data.Array, τxy::Data.Array, Exx::Data.Array, Eyy::Data.Array, Exy::Data.Array, etan::Data.Array, Gdτ::Data.Array)\n",
    "    @all(τxx)     = ( @all(τxx) + 2.0*@all(Gdτ)*@all(Exx) )  /  ( @all(Gdτ)/@all(etan) + 1.0 )\n",
    "    @all(τyy)     = ( @all(τyy) + 2.0*@all(Gdτ)*@all(Eyy) )  /  ( @all(Gdτ)/@all(etan) + 1.0 )\n",
    "    @all(τxy)     = ( @all(τxy) + 2.0*@all(Gdτ)*@all(Exy) )  /  ( @all(Gdτ)/@all(etan) + 1.0 )\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function compute_Pτgrad!(dPdx::Data.Array, dPdy::Data.Array, dτxxdx::Data.Array, dτyydy::Data.Array, dτxydx::Data.Array, dτxydy::Data.Array, P::Data.Array, τxx::Data.Array, τyy::Data.Array, τxy::Data.Array, dx::Data.Number, dy::Data.Number)\n",
    "    @all(dPdx)    = @d_xa(P)  /dx\n",
    "    @all(dPdy)    = @d_ya(P)  /dy\n",
    "    @all(dτxxdx)  = @d_xa(τxx)/dx\n",
    "    @all(dτyydy)  = @d_ya(τyy)/dy\n",
    "    @all(dτxydx)  = @d_xa(τxy)/dx\n",
    "    @all(dτxydy)  = @d_ya(τxy)/dy\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function compute_dV!(Rx::Data.Array, Ry::Data.Array, dVx::Data.Array, dVy::Data.Array, dPdx::Data.Array, dPdy::Data.Array, dτxxdx::Data.Array, dτyydy::Data.Array, dτxydx::Data.Array, dτxydy::Data.Array, dτ_Rho::Data.Array, PI2::Data.Number, rho_Rock::Data.Array, rho_sc::Data.Number)\n",
    "    @all(Rx)      = - @av_ya(dPdx) + PI2 * @av_xa(dτxydy) + PI2 * @av_ya(dτxxdx)\n",
    "    @all(Ry)      = - @av_xa(dPdy) + PI2 * @av_ya(dτxydx) + PI2 * @av_xa(dτyydy) - PI2 * @av(rho_Rock) / rho_sc\n",
    "    @all(dVx)     =   @av(dτ_Rho)  * @all(Rx)\n",
    "    @all(dVy)     =   @av(dτ_Rho)  * @all(Ry)\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function compute_V!(Vx::Data.Array, Vy::Data.Array, dVx::Data.Array, dVy::Data.Array)\n",
    "    @inn(Vx)      = @inn(Vx) + @all(dVx)\n",
    "    @inn(Vy)      = @inn(Vy) + @all(dVy)\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel function boundary_value!(P_x::Data.Array, P_y::Data.Array, etan_x::Data.Array, etan_y::Data.Array, P::Data.Array, etan::Data.Array)\n",
    "    @all(P_x)     = @av_xa(P)\n",
    "    @all(P_y)     = @av_ya(P)\n",
    "    @all(etan_x)  = @av_xa(etan)\n",
    "    @all(etan_y)  = @av_ya(etan)\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel_indices (ix) function TopBottom!(Vy::Data.Array, dy::Data.Number, p_water::Data.Number, p_NVP::Data.Number, P_x::Data.Array, etan_x::Data.Array)\n",
    "    Vy[ix+1, 1]   = Vy[ix+1, 2]     - dy * ( - p_water - p_NVP + P_x[ix, 1] ) / etan_x[ix, 1]   / 2\n",
    "    Vy[ix+1, end] = Vy[ix+1, end-1] + dy * ( - p_water + P_x[ix, end]       ) / etan_x[ix, end] / 2\n",
    "    return\n",
    "end\n",
    "\n",
    "@parallel_indices (iy) function LeftRight!(Vx::Data.Array, dx::Data.Number, p_NMHP::Data.Number, P_y::Data.Array, etan_y::Data.Array, DL::Data.Array)\n",
    "    Vx[1,iy+1]    = Vx[2, iy+1]     - dx * ( - p_NMHP * DL[1,iy+1]   + P_y[1, iy]   ) / etan_y[1,iy]   / 2\n",
    "    Vx[end,iy+1]  = Vx[end-1, iy+1] + dx * ( - p_NMHP * DL[end,iy+1] + P_y[end, iy] ) / etan_y[end,iy] / 2 \n",
    "    return\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "## CREATE OTHER FUNCTIONS TO BE USED OUTSIDE THE PSEUDO-TRANSIENT LOOP\n",
    "# Smooth the viscosity\n",
    "function smooth(A::Array, factor::Number)\n",
    "    m, n  = size(A)\n",
    "    A2    = zeros(m, n)\n",
    "    for i = 2:m-1\n",
    "        for j = 2:n-1\n",
    "            A2[i, j] = A[i, j] + 1.0/4.1/factor * (A[i-1, j] - 2 * A[i, j] + A[i+1, j] + A[i, j-1] - 2 * A[i, j] + A[i, j+1])\n",
    "        end\n",
    "    end\n",
    "    # Copy boundary values\n",
    "    A2[1, :]   = A[1, :]\n",
    "    A2[end, :] = A[end, :]\n",
    "    A2[:, 1]   = A[:, 1]\n",
    "    A2[:, end] = A[:, end]\n",
    "    return A2\n",
    "end\n",
    "\n",
    "# Compute the location of the maximum viscosity of the grid and the grids adjacent to it\n",
    "function compute_maxloc(matrix::Matrix)\n",
    "    rows, cols = size(matrix)\n",
    "    result     = copy(matrix)\n",
    "    for r = 1:rows\n",
    "        for c = 1:cols\n",
    "            neighbors    = [\n",
    "                matrix[max(r-1, 1)   , c             ], \n",
    "                matrix[min(r+1, rows), c             ], \n",
    "                matrix[r             , max(c-1, 1)   ], \n",
    "                matrix[r             , min(c+1, cols)], \n",
    "                matrix[r             , c             ]\n",
    "            ]\n",
    "            result[r, c] = maximum(neighbors)\n",
    "        end\n",
    "    end\n",
    "    return result\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "## MAIN FUNCTION\n",
    "@views function SaltInclusion2D()\n",
    "    # Physics\n",
    "    A_Salt      = 1e17                          # Salt viscosity [Pa-s]\n",
    "    A_Shale     = 1e15                          # Shale viscosity [Pa-s]\n",
    "    g           = 9.81                          # Gravity [m/s2]\n",
    "    p_grad_h    = 0.75*22620.6                  # Minimum horizontal stress gradient [Pa/m]\n",
    "    p_grad_v    = 0.85*22620.6                  # Vertical stress gradient [Pa/m]\n",
    "    rho_Shale   = p_grad_v/g                    # Bulk Shale density [kg/m3]\n",
    "    rho_Salt    = 2200                          # Salt density [kg/m3]\n",
    "    rho_Water   = 1020                          # Water density [kg/m3]\n",
    "    D_sf        = 800                           # Seafloor depth [m]\n",
    "    \n",
    "    # Input dimensions\n",
    "    Ly          = 10000                         # Domain's Y-length\n",
    "    aspect      = 3                             # Aspect ratio\n",
    "    \n",
    "    # Scales\n",
    "    rho_sc      = rho_Shale                     # Density scale\n",
    "    eta_sc      = A_Shale                       # Rock viscosity scale\n",
    "    L_sc        = ((eta_sc^2)/g/(rho_sc^2))^(1/3)  # Length scale\n",
    "    PI2         = g*(L_sc^3)*(rho_sc^2)/(eta_sc^2) # Make PI2 = 1 to stabilize\n",
    "    Ly          = Ly/L_sc                       # Non-dimensionalized Y-length\n",
    "    v_sc        = rho_sc*g*(L_sc^2)/eta_sc      # Velocity scale\n",
    "    p_sc        = (eta_sc^2)/(rho_sc*L_sc^2)    # Pressure scale\n",
    "    p_water     = rho_Water*g*D_sf/p_sc         # Seawater pressure at the top boundary\n",
    "    p_NMHP      = p_grad_h*Ly*L_sc/p_sc         # Nondimensionalized minimum horizontal pressure\n",
    "    p_NVP       = p_grad_v*Ly*L_sc/p_sc         # Nondimensionalized vertical pressure\n",
    "    \n",
    "    # Numerics\n",
    "    nx          = 50                            # Number of blocks in X-direction\n",
    "    ny          = 50                            # Number of blocks in Y-direction\n",
    "    epsi        = 1e-8                          # Tolerance\n",
    "    niter       = 5e7                           # Maximum iterations\n",
    "    nout_iter   = 1000                          # Output iterations\n",
    "    CFL         = 0.9/sqrt(2)                   # Courant-Friedrichs-Lewy\n",
    "    Re          = 3/2*sqrt(10)*pi               # Reynolds number\n",
    "    r           = 1.0                           # Bulk to shear modulus ratio\n",
    "    err         = 2*epsi                        # Initialize errors\n",
    "    err_evo1    = []\n",
    "    err_evo2    = []\n",
    "    iter        = 0                             # Initialize iteration\n",
    "    \n",
    "    # Pre-processing\n",
    "    Lx          = aspect * Ly                   # X-length\n",
    "    dx          = Lx/(nx-1)                     # Block X-size\n",
    "    dy          = Ly/(ny-1)                     # Block Y-size\n",
    "    xn          = -dx/2:dx:Lx+dx/2              # Grid X-location\n",
    "    yn          = -dy/2:dy:Ly+dy/2              # Grid Y-location\n",
    "    xc          = 0:dx:Lx                       # Block center X-location\n",
    "    yc          = 0:dy:Ly                       # Block center Y-location\n",
    "    max_lxy     = max(Lx, Ly)                   # Maximum block length\n",
    "    Vpdτ        = min(dx, dy) * CFL             # P-wave velocity * pseudo time step\n",
    "    Vx          = @zeros(nx+1, ny+1)            # Initialize Velocity-X\n",
    "    Vy          = @zeros(nx+1, ny+1)            # Initialize Velocity-Y\n",
    "    P           = @zeros(nx  , ny  )            # Initialize Total Pressure\n",
    "    τxx         = @ones (nx  , ny  )            # Initialize Deviatoric Stress XX\n",
    "    τyy         = @ones (nx  , ny  )            # Initialize Deviatoric Stress YY\n",
    "    τxy         = @zeros(nx  , ny  )            # Initialize Deviatoric Stress XY\n",
    "    divV        = @zeros(nx  , ny  )            # Initialize Divergence of Velocity\n",
    "    dVxdx       = @zeros(nx  , ny+1)            # Initialize dVx/dx\n",
    "    dVydy       = @zeros(nx+1, ny  )            # Initialize dVy/dy\n",
    "    dVxdy       = @zeros(nx+1, ny  )            # Initialize dVx/dy\n",
    "    dVydx       = @zeros(nx  , ny+1)            # Initialize dVy/dx\n",
    "    Exx         = @zeros(nx  , ny  )            # Initialize Strain XX\n",
    "    Eyy         = @zeros(nx  , ny  )            # Initialize Strain YY\n",
    "    Exy         = @zeros(nx  , ny  )            # Initialize Strain XY\n",
    "    dPdx        = @zeros(nx-1, ny  )            # Initialize dP/dx\n",
    "    dPdy        = @zeros(nx  , ny-1)            # Initialize dP/dy\n",
    "    dτxxdx      = @zeros(nx-1, ny  )            # Initialize dτxx/dx\n",
    "    dτyydy      = @zeros(nx  , ny-1)            # Initialize dτyy/dy\n",
    "    dτxydx      = @zeros(nx-1, ny  )            # Initialize dτxy/dx\n",
    "    dτxydy      = @zeros(nx  , ny-1)            # Initialize dτxy/dy\n",
    "    Rx          = @zeros(nx-1, ny-1)            # Initialize Residual in the X-direction\n",
    "    Ry          = @zeros(nx-1, ny-1)            # Initialize Residual in the Y-direction \n",
    "    dVx         = @zeros(nx-1, ny-1)            # Initialize Velocity-X Difference during convergence\n",
    "    dVy         = @zeros(nx-1, ny-1)            # Initialize Velocity-Y Difference during convergence\n",
    "    P_x         = @zeros(nx-1, ny  )            # Initialize Pressure matrix after averaging adjacent row values\n",
    "    P_y         = @zeros(nx  , ny-1)            # Initialize Pressure matrix after averaging adjacent column values\n",
    "    etan        = @ones (nx  , ny  )            # Initialize Rock Viscosity (Shale)\n",
    "    etan_x      = @zeros(nx-1, ny  )            # Initialize Viscosity matrix after averaging adjacent row values\n",
    "    etan_y      = @zeros(nx  , ny-1)            # Initialize Viscosity matrix after averaging adjacent column values\n",
    "    DL          = @zeros(nx+1, ny+1)            # Initialize Dimensionless Length, DL = grid depth / domain depth, DL at seafloor = 0\n",
    "    DL[1,:]     = LinRange(1, 0, ny+1)          \n",
    "    \n",
    "    # Salt location and density\n",
    "    SaltFraction = zeros(nx,ny)                 # Initialize Salt Fraction\n",
    "    img = load(\"SaltGeometry2D.JPG\")            # Load the Salt Geometry\n",
    "    gray_img = Gray.(img)                       # Convert the image to grayscale\n",
    "    rotated_img = imrotate(gray_img, pi/2)      # Rotate the grayscale image by 90 degrees\n",
    "    bw_float = channelview(rotated_img)         # Convert the rotated image to binary (black and white)\n",
    "    bw = bw_float .> 0.5                        # Threshold the binary image at 0.5 to get a binary image\n",
    "    resized_img = imresize(bw, (nx, ny))        # Resize the binary image to have dimensions [nx, ny]\n",
    "    SaltFraction .= resized_img .< 0.5          # Invert the resized image and assign to SaltFraction\n",
    "\n",
    "    # Rock Viscosity\n",
    "    etan[SaltFraction.==1.0] .= A_Salt/A_Shale  # Replace Shale Viscosity by Salt Viscosity\n",
    "    \n",
    "    # Initialize Rock Density\n",
    "    rho_Rock    = fill(rho_Shale, nx, ny)       # Initialize Shale Density\n",
    "    rho_Rock[SaltFraction.==1.0] .= rho_Salt    # Initialize Salt Density\n",
    "    \n",
    "    # Smear out the coefficients of Stokes equation\n",
    "    for ism = 1:10\n",
    "        etan = smooth(etan, 1.0)                # Smoothed Rock Viscosity\n",
    "    end\n",
    "    \n",
    "    # Calculate the Maximum Rock Viscosity Among Neighbors\n",
    "    etanmax         = compute_maxloc(etan)\n",
    "    etanmax[1, :]   = etanmax[2, :]\n",
    "    etanmax[end, :] = etanmax[end - 1, :]\n",
    "    etanmax[:, 1]   = etanmax[:, 2]\n",
    "    etanmax[:, end] = etanmax[:, end - 1]\n",
    "    \n",
    "    # Pre-processing of numerics\n",
    "    dτ_Rho = zeros(nx, ny)                      # Pseudo Time Step / Density\n",
    "    dτ_Rho = Vpdτ * max_lxy / Re ./ etanmax\n",
    "    Gdτ    = zeros(nx, ny)                      # G * Pseudo Time Step\n",
    "    Gdτ    = (Vpdτ^2) ./ dτ_Rho / (r+2)\n",
    "    \n",
    "    \n",
    "    # Pseudo-Transient Iterations\n",
    "    while err > epsi && iter <= niter\n",
    "        # Compute Velocity Gradients\n",
    "        @parallel compute_Vgrad!(dVxdx, dVydy, dVxdy, dVydx, Vx, Vy, dx, dy)\n",
    "        \n",
    "        # Compute Pressure\n",
    "        @parallel compute_P!(divV, P, dVxdx, dVydy, Gdτ, r, dx, dy)\n",
    "        \n",
    "        # Compute Strain\n",
    "        @parallel compute_E!(Exx, Eyy, Exy, dVxdx, dVydy, dVxdy, dVydx)\n",
    "        \n",
    "        # Compute Stress\n",
    "        @parallel compute_τ!(τxx, τyy, τxy, Exx, Eyy, Exy, etan, Gdτ)\n",
    "        \n",
    "        # Compute Gradients of Pressure and Deviatoric Stress \n",
    "        @parallel compute_Pτgrad!(dPdx, dPdy, dτxxdx, dτyydy, dτxydx, dτxydy, P, τxx, τyy, τxy, dx, dy)\n",
    "        \n",
    "        # Compute Residuals of Stokes Equation\n",
    "        @parallel compute_dV!(Rx, Ry, dVx, dVy, dPdx, dPdy, dτxxdx, dτyydy, dτxydx, dτxydy, dτ_Rho, PI2, rho_Rock, rho_sc)\n",
    "        \n",
    "        # Compute Velocity\n",
    "        @parallel compute_V!(Vx::Data.Array, Vy::Data.Array, dVx::Data.Array, dVy::Data.Array)\n",
    "        \n",
    "        # Apply Boundary Conditions\n",
    "        @parallel boundary_value!(P_x, P_y, etan_x, etan_y, P, etan)\n",
    "        @parallel (1:size(P_x,1)) TopBottom!(Vy, dy, p_water, p_NVP, P_x, etan_x)\n",
    "        @parallel (1:size(P_y,2)) LeftRight!(Vx, dx, p_NMHP, P_y, etan_y, DL)\n",
    "\n",
    "        # Update iteration\n",
    "        iter = iter + 1\n",
    "        \n",
    "        # Compare error and tolerance\n",
    "        if iter % nout_iter == 0\n",
    "            Vmin    = minimum(Vx)\n",
    "            Vmax    = maximum(Vx)\n",
    "            Pmin    = minimum(P)\n",
    "            Pmax    = maximum(P)\n",
    "            norm_Rx = norm(Rx  ) / (Pmax - Pmin) * Lx / sqrt(length(Rx)  )\n",
    "            norm_Ry = norm(Ry  ) / (Pmax - Pmin) * Lx / sqrt(length(Ry)  )\n",
    "            norm_dV = norm(divV) / (Vmax - Vmin) * Lx / sqrt(length(divV))\n",
    "            err     = maximum([norm_Rx, norm_Ry, norm_dV])\n",
    "            push!(err_evo1, err )\n",
    "            push!(err_evo2, iter)\n",
    "            println(\"Total steps = $iter, err = $err [norm_Rx=$norm_Rx, norm_Ry=$norm_Ry, norm_dV=$norm_dV]\")\n",
    "        end\n",
    "        \n",
    "        # Make plots\n",
    "        if iter % nout_iter == 1\n",
    "            p1 = heatmap(xc, yc, (Array(τxx))'  , aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Deviatoric Stress τxx\")\n",
    "            p2 = heatmap(xc, yc, (Array(τyy))'  , aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Deviatoric Stress τyy\")\n",
    "            p3 = heatmap(xc, yc, (Array(τxy))'  , aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Deviatoric Stress τxy\")\n",
    "            p4 = heatmap(xc, yc, (Array(P))'    , aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Pressure\"             )\n",
    "            p5 = heatmap(xn, yn, (Array(Vx))'   , aspect_ratio=1, xlims=extrema(xn), ylims=extrema(yn), c=:viridis, title=\"Velocity Vx\"          )\n",
    "            p6 = heatmap(xn, yn, (Array(Vy))'   , aspect_ratio=1, xlims=extrema(xn), ylims=extrema(yn), c=:viridis, title=\"Velocity Vy\"          )\n",
    "            p7 = heatmap(xc, yc, (Array(τxx+P))', aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Stress Sxx\"           )\n",
    "            p8 = heatmap(xc, yc, (Array(τyy+P))', aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Stress Syy\"           )\n",
    "            p9 = heatmap(xc, yc, (Array(etan))' , aspect_ratio=1, xlims=extrema(xc), ylims=extrema(yc), c=:viridis, title=\"Viscosity\"            )\n",
    "            display(plot(p1, p2, p3, p4, p5, p6, p7, p8, p9, layout=(3,3)))\n",
    "        end\n",
    "        \n",
    "    end\n",
    "end\n",
    "\n",
    "## RUN CODE\n",
    "SaltInclusion2D = begin SaltInclusion2D();\n",
    "return;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
